/*
 * Copyright (c) 2017 Informatics Matters Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'Core services'
def contextName = 'coreservices'

apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'org.unbroken-dome.test-sets'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage



dependencies {
    
    compile project(':common')
    compile project(':common-utils')
    compile project(':common-camel')
    compile project(':chemaxon-lib')
    compile project(':core-services-common')
    compile project(':core-services-exec')
    compile project(':core-services-client')
    
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"

    compile "org.apache.camel:camel-core:$camelVersion"
    compile "org.apache.camel:camel-jackson:$camelVersion"
    compile "org.apache.camel:camel-servletlistener:$camelVersion"
    compile "org.apache.camel:camel-servlet:$camelVersion"
    compile "org.apache.camel:camel-swagger:$camelVersion"
    compile "org.apache.camel:camel-http4:$camelVersion"
    compile "org.apache.camel:camel-rabbitmq:$camelVersion"

    compile "org.postgresql:postgresql:$postgresDriverVersion"
     
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'

    testCompile project(':core-test')
    
    def tomcatVersion = '7.0.59'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
    

  
}

/* Integration test setup is borrowed from here:
 * http://www.petrikainulainen.net/programming/gradle/getting-started-with-gradle-integration-testing-with-the-testsets-plugin/
 * */
testSets {
    integrationTest
}

//check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

integrationTest {
    dependsOn rootProject.tasks.integrationTestEnvPrepare
    finalizedBy rootProject.tasks.integrationTestEnvShutdown
}

project.integrationTest {
    outputs.upToDateWhen { false }
}

tomcat {
    enableSSL = true
    contextPath = contextName
    
    users {
        user {
            username = 'admin'
            password = 'lacrocks'
            roles = ['developers', 'admin']
        }

        user {
            username = 'manager'
            password = 'lacrocks'
            roles = ['manager']
        }
    }
}

/** Build a Docker image
 * run the generated image from the docker/deploy directory with something like this:
 * 
 * docker-compose run coreservices
 */

docker {
    println "Using docker host of $dockerHost"
    url = dockerHost
    certPath = dockerCertPath
}

task buildDockerfile(type: Dockerfile) {

    destFile = project.file('build/Dockerfile')
    from "informaticsmatters/tomcat:8.0-jre8"

    label(['maintainer': 'Tim Dudgeon <tdudgeon@informaticsmatters.com>'])
    label(['commit-hash': "${imageCommitHash}"])

    runCommand("rm -rf /usr/local/tomcat/webapps/*")
    
    def warFile = tasks.war.outputs.files.singleFile
    addFile('libs/' + warFile.name, "/usr/local/tomcat/webapps/${contextName}.war")
    
    exposePort(8080)
}

task buildDockerImage(type: DockerBuildImage) {

    description = "Build the coreservices docker image"

    dependsOn = ['war', 'buildDockerfile']

    inputDir = buildDockerfile.destFile.parentFile
    tag = "squonk/${contextName}:${dockerImageTag}"
}
