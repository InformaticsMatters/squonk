/*
 * Copyright (c) 2017 Informatics Matters Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'RDKit database search'
def contextName = 'chemcentral-search'

apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage


dependencies {
    compile project(':common')
    compile project(':common-camel')
    compile project(':core-services-common')

    compile project(':rdkit-lib')
    compile project(':rdkit-databases')

    compile "org.jboss.weld.servlet:weld-servlet-core:$weldVersion"
    
    compile "org.apache.camel:camel-core:$camelVersion"
    compile "org.apache.camel:camel-jackson:$camelVersion"
    compile "org.apache.camel:camel-servlet:$camelVersion"
    compile "org.apache.camel:camel-servletlistener:$camelVersion"
    compile "org.apache.camel:camel-swagger:$camelVersion"
    compile "org.apache.camel:camel-swagger-java:$camelVersion"
    compile "org.apache.camel:camel-cdi:$camelVersion"
    compile "org.apache.camel:camel-rabbitmq:$camelVersion"
    compile "org.apache.camel:camel-http4:$camelVersion"
    
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'

    def tomcatVersion = '7.0.59'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"

}

tomcat {
    enableSSL = false
}

docker {
    url = dockerHost
    certPath = dockerCertPath
}

task buildDockerfile(type: Dockerfile) {

    destFile = project.file('build/Dockerfile')
    from "informaticsmatters/rdkit-tomcat-debian:Release_2020_03_04"

    label(['maintainer': 'Tim Dudgeon <tdudgeon@informaticsmatters.com>'])
    label(['commit-hash': "${imageCommitHash}"])

    runCommand("rm -rf /usr/local/tomcat/webapps/*")

    def warFile = tasks.war.outputs.files.singleFile
    addFile('libs/' + warFile.name, "/usr/local/tomcat/webapps/${contextName}.war")

    exposePort(8080)
}

task buildDockerImage(type: DockerBuildImage) {

    description = "Build the chemservices rdkit search docker image"

    dependsOn = [war, buildDockerfile]

    inputDir = buildDockerfile.destFile.parentFile
    tag = "squonk/${contextName}:${dockerImageTag}"
}
