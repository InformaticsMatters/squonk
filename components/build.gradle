/*
 * Copyright (c) 2018 Informatics Matters Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'Squonk tools for life sciences'

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.2'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.12' // needs gradle >= 2.5

        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0'
        classpath 'org.ajoberstar:gradle-git:1.7.2'
    }
}

allprojects {

    group = 'org.squonk.components'

    apply plugin: 'groovy' // Apply the groovy plugin (also adds support for Java)
    apply plugin: 'maven'  // Maven repo stuff
    apply plugin: 'org.ajoberstar.grgit' // Git repo tools (grgit.branch.getCurrent())
    apply plugin: 'findbugs' // Java static analyser
    apply plugin: 'jacoco' // Code coverage

    sourceCompatibility = 1.8
    version = '0.2.0-SNAPSHOT'

    project.ext.set('camelVersion', '2.18.0')
    // look at the jackson2-version property here (with correct tag) to get the right Jackson version
    // https://github.com/apache/camel/blob/master/parent/pom.xml
    project.ext.set('jacksonVersion', '2.8.4')
    project.ext.set('cdkVersion', '2.2')
    project.ext.set('groovyVersion', '2.4.4')
    project.ext.set('weldVersion', '2.3.2.Final')
    project.ext.set('postgresDriverVersion', '9.4-1201-jdbc41')
    project.ext.set('flywayVersion', '3.2.1')

    // Define the tag label for docker images.
    // If SQUONK_IMAGE_TAG is set then it is used,
    // otherwise it's the Git branch name or, if on master
    // it's either 'latest' or the value of our 'version' property.
    def dockerImageTag = System.getenv('SQUONK_IMAGE_TAG')
    if (dockerImageTag == null || dockerImageTag.length() == 0) {
        dockerImageTag = grgit.branch.getCurrent().name
        if (dockerImageTag == 'master') {
            // As we're here SQUONK_IMAGE_TAG is not set.
            // It's the master branch so the docker image tag should be...
            // the version number if it's not a SNAPSHOT otherwise 'latest'
            if (!version.endsWith('-SNAPSHOT')) {
                dockerImageTag = version
            } else {
                dockerImageTag = 'latest'
            }
        }
    }
    project.ext.set('dockerImageTag', dockerImageTag)

    project.ext.set('imageCommitHash', grgit.head().abbreviatedId)

    // In this section you declare where to find the dependencies of your project
    repositories {

        jcenter()

        maven {
            url cxnMavenRepositoryUrl
            credentials {
                username = cxnMavenUser
                password = cxnMavenPassword
            }
        }

        maven {
            url "https://nexus.ideaconsult.net/content/repositories/thirdparty"
        }

    }

    // In this section you declare the dependencies for your production and test code
    dependencies {
        compile 'org.slf4j:slf4j-jdk14:1.7.10'

        // Spock for unit testing
        testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
        testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    }

    // Configure FindBugs (static code analysis)
    findbugs {
        reportsDir = file("$project.buildDir/findbugsReports")
        ignoreFailures = true
        reportLevel = 'high'
        effort = 'max'
    }
    tasks.withType(FindBugs) {
        task -> enabled = gradle.startParameter.taskNames.contains(task.name)
        reports {
            xml.enabled true
            html.enabled false
        }
    }

    // Configure JaCoCo (code coverage)
    jacocoTestReport {
        group = "Reporting"
        reports {
            xml.enabled true
            csv.enabled false
            html.destination file("${buildDir}/reports/coverage")
        }
    }

}

// all subprojects are assumed to have artifacts to send to maven repo
subprojects {
    uploadArchives {
        repositories {
            mavenDeployer {
                // hard coded URL as Jenkins puts things to there.
                // If you want to run this locally then make sure that directory exists. 
                repository(url: "file:///var/maven_repo/")
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5.1'
}

project.ext.set('dockerHost', getDockerHost())
project.ext.set('dockerCertPath', System.env.DOCKER_CERT_PATH ? file(System.env.DOCKER_CERT_PATH) : null)

// Install (decrypt) the ChemAxon licence file.
// This is expected in the project's encrypted/chemaxon directory.
// SQUONK_DECRYPTION_KEY is expected to contain the decryption key.
task installChemaxonLicense(type: Exec) {
    workingDir '../encrypted/chemaxon'
    commandLine  'bash', '-c',
        'mkdir -p ../../data/licenses &' +
        ' echo "$SQUONK_DECRYPTION_KEY"' +
        ' | gpg --batch --yes --passphrase-fd 0' +
        '   --output ../../data/licenses/license.cxl' +
        '   --decrypt license.cxl.gpg'
}

task installChemaxonLicenseToHome(type: Exec) {
    workingDir '../encrypted/chemaxon'
    commandLine  'bash', '-c',
            'mkdir -p "$HOME"/.chemaxon &' +
            ' echo "$SQUONK_DECRYPTION_KEY"' +
            ' | gpg --batch --yes --passphrase-fd 0' +
            '   --output "$HOME"/.chemaxon/license.cxl' +
            '   --decrypt license.cxl.gpg'
}

// Install (decrypt) the ChemAxon reaction library.
// This is expected in the project's encrypted/chemaxon directory.
// SQUONK_DECRYPTION_KEY is expected to contain the decryption key.
task installChemaxonLibrary(type: Exec) {
    workingDir '../encrypted/chemaxon'
    commandLine  'bash', '-c',
        'echo "$SQUONK_DECRYPTION_KEY"' +
        ' | gpg --batch --yes --passphrase-fd 0' +
        '   --output ../../docker/deploy/images/chemservices/chemaxon_reaction_library.zip ' +
        '   --decrypt chemaxon_reaction_library.zip.gpg'
}

task installCpSignLicense(type: Copy) {
    from "$System.env.HOME/.squonk/cpsign0.3pro.license"
    into '../data/licenses'
}

task installLicenses {

    description = "Copies various license files into place"
    // Here the license file origin is expected to be the user's $HOME/.squonk directory.
    // The task simply invokes a number of individual copy tasks to move the
    // files into their expected location within the project.

    dependsOn = [
        'installChemaxonLicense',
        'installChemaxonLicenseToHome',
        'installChemaxonLibrary',
        'installCpSignLicense'
    ]

}

String getDockerHost() {
    String h = System.getenv('DOCKER_HOST') ?: 'unix:///var/run/docker.sock'
    return h
}

docker {
    println "Using docker host of $dockerHost"
    url = dockerHost
    certPath = dockerCertPath
}

task chemServicesWars(type: Copy) {
    dependsOn = [
            tasks.getByPath(':chem-services-cdk-basic:war'),
            tasks.getByPath(':chem-services-chemaxon-basic:war'),
            tasks.getByPath(':chem-services-rdkit-basic:war'),
            tasks.getByPath(':chem-services-rdkit-search:war'),
            tasks.getByPath(':chem-services-openchemlib-basic:war'),
            tasks.getByPath(':chem-services-smartcyp:war')
    ]

    from "../docker/deploy/images/chemservices/chemaxon_reaction_library.zip"
    from("chem-services-cdk-basic/build/libs") {
        include '*.war'
        rename '.*', 'chem-services-cdk-basic.war'
    }
    from("chem-services-chemaxon-basic/build/libs") {
        include '*.war'
        rename '.*', 'chem-services-chemaxon-basic.war'
    }
    from("chem-services-rdkit-basic/build/libs") {
        include '*.war'
        rename '.*', 'chem-services-rdkit-basic.war'
    }
    from("chem-services-openchemlib-basic/build/libs") {
        include '*.war'
        rename '.*', 'chem-services-openchemlib-basic.war'
    }
    from("chem-services-smartcyp/build/libs") {
        include '*.war'
        rename '.*', 'chem-services-smartcyp.war'
    }
    into 'build/chem-services-basic'
}

task dockerFileChemServices(type: Dockerfile) {

    destFile = project.file('build/chem-services-basic/Dockerfile')
    // if changing the version you must also update the hardcoded version number in org.squonk.camel.rdkit.processor.RDKitMoleculeProcessor
    from "informaticsmatters/rdkit-tomcat-debian:Release_2018_09_1"

    label(['maintainer': 'Tim Dudgeon <tdudgeon@informaticsmatters.com>'])
    label(['commit-hash': "${imageCommitHash}"])

    addFile('*.war', "/usr/local/tomcat/webapps/")
    addFile('chemaxon_reaction_library.zip', '/chemaxon_reaction_library.zip')

    exposePort(8080)
}

task dockerImageChemServices(type: DockerBuildImage) {

    description = 'Builds the ChemServices Docker Image'

    dependsOn = [chemServicesWars, dockerFileChemServices]

    inputDir = dockerFileChemServices.destFile.parentFile
    tag = "squonk/chemservices-basic:${dockerImageTag}"
}


task dockerBuildImages() {

    dependsOn = [
        'dockerImageChemServices',                     // squonk/chemservices-basic
        'chem-services-rdkit-search:buildDockerImage', // squonk/chemcentral-search
        'core-services-server:buildDockerImage',       // squonk/coreservices
        'cell-executor:dockerBuildImage',              // squonk/cellexecutor
        'job-executor:buildDockerImage',               // squonk/jobexecutor
        'rdkit-databases:dockerBuildImage',            // squonk/chemcentral-loader
        'database:buildDockerImage'                    // squonk/flyway
    ]
}

/*
Running integration tests is complex and requires some setup before they can be run.
Much of the material is in the /docker directory.

Before running you must do these steps (some might be missing - please report)
1. create a /var/maven_repo directory on your system. This is needed by the uploadArchives task
2. create a /squonk/work directory (and make sure that is accessible to Docker e.g. on Docker for
Mac you need to add this to the 'File Sharing' options).
3. execute the docker/deploy/images-pull-extra.sh script to pull the required Docker images

Integration tests run against the production Docker images that need to be created prior to running
the tests. To do this preparation run the  integrationTestInit task which builds the necessary images
and does any other setup that is needed. If the running code (as opposed to the test code) changes this
step needs to be repeated before running the tests.

To run the integration tests run the integrationTest task.

 */
task integrationTestInit() {
    dependsOn = [uploadArchives, dockerBuildImages]
}

task integrationTestEnvPrepare(type: Exec) {
    description 'Setup integration test environment'
    //dependsOn 'dockerFileChemServices', 'core-services-server:buildDockerFile', 'cell-executor:dockerBuildImage'

    workingDir '../docker'
    commandLine 'bash', 'int-test-env-prepare.sh', dockerImageTag
}

task integrationTestEnvShutdown(type: Exec) {
    workingDir '../docker'
    commandLine 'bash', 'int-test-env-shutdown.sh'
}

task integrationTestEnvClean(type: Exec) {
    commandLine 'bash', '../docker/int-test-env-clean.sh'
}

// tests integration test setup process without running any tests
task integrationTestEnvDryRun {
    dependsOn integrationTestEnvPrepare
    finalizedBy integrationTestEnvShutdown
}

task dbTestEnvPrepare(type: Exec) {
    workingDir '../docker'
    commandLine 'bash', 'db-test-env-prepare.sh'
}

task dbTestEnvShutdown(type: Exec) {
    workingDir '../docker'
    commandLine 'bash', 'db-test-env-shutdown.sh'
}
